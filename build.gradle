buildscript {
    
    ext {
        versions = [
                kotlin            : "1.4.21",
                appcompat         : "1.2.0",
                corektx           : "1.3.2",
                material          : "1.3.0",
                constraintlayout  : "2.0.4",
                junit             : "4.13.2",
                junitext          : "1.1.2",
                espressocore      : "3.3.0"
        ]

        androidx = [
                appcompat           : "androidx.appcompat:appcompat:${versions.appcompat}",
                corektx             : "androidx.core:core-ktx:${versions.corektx}",
                constraintlayout    : "androidx.constraintlayout:constraintlayout:${versions.constraintlayout}",
                junitext            : "androidx.test.ext:junit:${versions.junitext}",
                espressocore        : "androidx.test.espresso:espresso-core:${versions.espressocore}"
        ]

        kotlinLib = [
                stdlib              : "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"
        ]

        google = [
                material            : "com.google.android.material:material:${versions.material}"
        ]

        junitLib = [
                junit               : "junit:junit:${versions.junit}"
        ]
    }

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.1.3"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.16.0"
    id "org.jlleitschuh.gradle.ktlint" version "9.2.1"
}

subprojects{
    apply {
        plugin("io.gitlab.arturbosch.detekt")
        plugin("org.jlleitschuh.gradle.ktlint")
    }

    // Optionally configure plugin
    ktlint {
        debug.set(true)
        version.set("0.37.2")
        verbose.set(true)
        android.set(false)
        outputToConsole.set(true)
        ignoreFailures.set(false)
        enableExperimentalRules.set(false)
        additionalEditorconfigFile = file(".editorconfig")
        filter {
            exclude("**/generated/**")
            include("**/kotlin/**")
        }

    }

    detekt {
        config = rootProject.files("config/detekt/detekt.yml")
        reports {
            html {
                enabled = true
                destination = file("build/reports/detekt.html")
            }
        }
    }

}

allprojects {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}